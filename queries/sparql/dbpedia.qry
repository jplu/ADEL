PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?p (GROUP_CONCAT(DISTINCT ?o;separator="-----") AS ?vals) ?id ?pr ?link WHERE {
    {
        SELECT DISTINCT ?link (STR(?o3) AS ?id) (STR(?o2) AS ?pr) WHERE {
            ?link dbo:wikiPageRank ?o2 .
            ?link dbo:wikiPageID ?o3 .
            FILTER NOT EXISTS{?link dbo:wikiPageRedirects ?x} .
            FILTER NOT EXISTS{?link dbo:wikiPageDisambiguates ?y} .
        } LIMIT 1 OFFSET %offset
    }
    {
        ?link ?p ?o .
        FILTER(DATATYPE(?o) = xsd:string || LANG(?o) = "en") .
    } UNION {
        VALUES ?p {dbo:wikiPageRedirects dbo:wikiPageDisambiguates} .
        ?x ?p ?link .
        ?x rdfs:label ?o .
    } UNION {
        VALUES ?p {rdf:type} .
        ?link ?p ?o .
        FILTER(CONTAINS(STR(?o), "http://dbpedia.org/ontology/")) .
    }
} GROUP BY ?p ?id ?pr ?link